import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
    id 'com.google.protobuf'
}

def getVersionCode = { ->
    try {
        def code = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--count', 'HEAD'
            standardOutput = code
        }
        return code.toString().trim().toInteger()
    } catch (ignored) {
        return -1
    }
}

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--dirty', '--always'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    } catch (ignored) {
        return null
    }
}

android {
    namespace 'de.stustapay.stustapay'
    compileSdk 33

    defaultConfig {
        applicationId "de.stustapay.stustapay"
        minSdk 27
        targetSdk 33
        versionCode getVersionCode()
        versionName getVersionName()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    signingConfigs {
        release {
            if (project.hasProperty('ANDROID_KEY_STORE_FILE')) {
                storeFile file(ANDROID_KEY_STORE_FILE)
                storePassword ANDROID_KEY_STORE_PASSWORD
                keyAlias ANDROID_KEY_ALIAS
                keyPassword ANDROID_KEY_PASSWORD
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (project.hasProperty('ANDROID_KEY_STORE_FILE')) {
                signingConfig signingConfigs.release
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.4.2'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}


dependencies {
    implementation project(":api")
    implementation project(":libssp")
    implementation 'androidx.core:core-ktx:1.10.1'
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation 'androidx.activity:activity-compose:1.7.2'
    implementation "androidx.compose.ui:ui:$compose_ui_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_ui_version"
    implementation 'androidx.compose.material:material:1.4.3'
    implementation 'androidx.navigation:navigation-compose:2.6.0'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation "androidx.camera:camera-core:$camerax_version"
    implementation "androidx.camera:camera-lifecycle:$camerax_version"
    implementation "androidx.camera:camera-view:$camerax_version"
    implementation 'com.google.zxing:core:3.5.1'
    implementation "androidx.camera:camera-camera2:$camerax_version"
    implementation "io.ktor:ktor-client-core:$ktor_version"
    implementation "io.ktor:ktor-client-cio:$ktor_version"
    implementation "io.ktor:ktor-client-content-negotiation:$ktor_version"
    implementation "io.ktor:ktor-serialization-kotlinx-json:$ktor_version"
    implementation "io.ktor:ktor-client-logging:$ktor_version"
    implementation 'androidx.databinding:databinding-runtime:8.0.2'
    implementation "androidx.datastore:datastore:1.0.0"
    implementation 'com.sumup:merchant-api:1.4.0'
    implementation 'com.sumup:merchant-sdk:5.0.0'
    implementation 'net.openid:appauth:0.11.1'
    implementation "com.google.protobuf:protobuf-javalite:$protobuf_version"
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_ui_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_ui_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_ui_version"
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"
    kapt "androidx.hilt:hilt-compiler:$hilt_android_version"
    annotationProcessor "androidx.hilt:hilt-compiler:$hilt_android_version"
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-compose:$lifecycle_version"
    implementation "com.ionspin.kotlin:bignum:0.3.9"
    implementation "com.ionspin.kotlin:bignum-serialization-kotlinx:0.3.2"
}

kapt {
    correctErrorTypes true
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:$protobuf_version"
    }

    // Generates the java Protobuf-lite code for the Protobufs in this project. See
    // https://github.com/google/protobuf-gradle-plugin#customizing-protobuf-compilation
    // for more information.
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }
        }
    }
}

android.defaultConfig.manifestPlaceholders = [
        'appAuthRedirectScheme': 'de.stustapay.stustapay'
]
